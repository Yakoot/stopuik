buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.2.0'
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.gretty:gretty:3.+"
    }
}

apply plugin: 'war'
apply plugin: 'kotlin'
apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'
apply plugin: 'org.gretty'

repositories {   // repositories for Jar's you access in your code
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.google.appengine:appengine-api-1.0-sdk:+'  // Latest App Engine Api's
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    compile "com.google.endpoints:endpoints-framework:2.+"
    // Postgres stuff
    compile 'org.postgresql:postgresql:42.+'
    compile 'com.zaxxer:HikariCP:2.+'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.10.1'
    compile group: 'org.jooq', name: 'jooq', version: '3.12.3'
    compile group: 'org.tuckey', name: 'urlrewritefilter', version: '4.0.4'
    compile group: 'org.telegram', name: 'telegrambots', version: '4.6'


    testCompile 'com.google.appengine:appengine-testing:+'
    testCompile 'com.google.appengine:appengine-api-stubs:+'
    testCompile 'com.google.appengine:appengine-tools-sdk:+'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task processFrontendResources {
    //dependsOn ":frontend:assembleFrontend"
    description "Copy client resources into build directories"
    doLast {
        delete "${projectDir}/src/main/webapp/js", "${projectDir}/src/main/webapp/css"
        copy {
            from file(project(':frontend').projectDir).absolutePath + "/dist"
            into "${projectDir}/src/main/webapp"
        }
    }
}

tasks.processResources {
  mustRunAfter "processFrontendResources"
}

tasks.war {
    dependsOn "processFrontendResources"
}

appengine {
    tools {
        cloudSdkHome = "/usr/lib/google-cloud-sdk"
    }
    deploy {
        projectId = "spbelect-blacklist-backend"
        //version = "staging"
        version = "2020-03-08"
    }
    run {
        jvmFlags = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"]
    }

}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
endpointsServer {
    // Endpoints Framework Plugin server-side configuration
    hostname = "spbelect-blacklist-backend.appspot.com"
}
